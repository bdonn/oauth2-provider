plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
}

ext {
    daggerVersion = '2.12'
    kotlinTestVersion = '3.4.0'
    koinVersion = '2.0.1'
}

apply plugin: 'application'

group 'today.malka'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    group 'today.malka'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.koin:koin-core:$koinVersion"
        implementation "org.koin:koin-core-ext:$koinVersion"

        api "com.google.dagger:dagger:$daggerVersion"
        annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

        testImplementation "org.koin:koin-test:$koinVersion"
        testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlinTestVersion"
        testImplementation "io.kotlintest:kotlintest-extensions-koin:$kotlinTestVersion"
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

project('auth-oauth2') {
    dependencies {
        implementation(project(':auth-account'))
    }
}

project('auth-oidc') {
    dependencies {
        implementation(project(':auth-account'))
        implementation(project(':auth-oauth2'))
    }
}

project('auth-server-ktor') {
    dependencies {
        implementation(project(':auth-oauth2'))
        implementation(project(':auth-oidc'))
    }
}

mainClassName = 'io.ktor.server.netty.EngineMain'
